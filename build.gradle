apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '2.3.5'
def pluginDescription = 'Plugin to enable HTTP basic authentication and/or Ip based authentication'
def pluginClassname = 'com.asquera.elasticsearch.plugins.http.HttpBasicServerPlugin'
def pluginName = 'elasticsearch-http-basic'
def elasticVersion = '2.3.5'

jar {
  jar.baseName = "elasticsearch-http-basic"
  manifest {
  	attributes 'Implementation-Title': 'elasticsearch-http-basic',
  	           'Implementation-Version': version
  }
}

configurations {
    wagon
    releaseJars {
        extendsFrom runtime
        exclude group: 'org.elasticsearch'
        exclude group: 'org.apache.lucene'
        exclude module: 'commons-logging'
        exclude module: 'commons-codec'
        exclude module: 'commons-lang3'
        exclude module: 'httpclient'
        exclude module: 'httpcore'
        exclude module: 'junit'
	}
}

repositories {
    mavenCentral()
}

dependencies {
    
    // elastic search
	compile("org.elasticsearch:elasticsearch:2.3.5")
	
	// httpclient
	compile("org.apache.httpcomponents:httpclient:4.5.2")
	
	// stuff
	compile("com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.3.4")
	
	//test
	testCompile group: 'junit', name: 'junit', version: '4.+'
	testCompile(group: 'org.elasticsearch', name: 'elasticsearch', version: '2.3.5', classifier: 'tests')
	testCompile(group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3')
	testCompile(group: 'org.apache.lucene', name: 'lucene-test-framework', version: '5.5.0')
    
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

task makePluginDescriptor(type: Copy) {
    from 'src/main/templates'
    into 'build/tmp/plugin'
    expand([
            'descriptor': [
                    'name': pluginName,
                    'classname': pluginClassname,
                    'description': pluginDescription,
                    'jvm': true,
                    'site': false,
                    'isolated': true,
                    'version': version,
                    'javaVersion': project.property('targetCompatibility'),
                    'elasticsearchVersion' : elasticVersion
            ]
    ])
}

task buildPluginZip(type: Zip, dependsOn: [':jar', ':makePluginDescriptor']) {
    from configurations.releaseJars
    from 'build/tmp/plugin'
    classifier = 'plugin'
}

task unpackPlugin(type: Copy, dependsOn: [':buildPluginZip']) {
    delete "plugins"
    from configurations.releaseJars
    from 'build/tmp/plugin'
    into "plugins/${pluginName}"
}

